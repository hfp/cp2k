$dtype = f64

func @dbm_multiply(%alpha: $dtype, %itask: i32,
                   %tasks: memref<i32x6x1>,
                   %A: memref<$dtype x1>,
                   %B: memref<$dtype x1>,
                   %C: memref<$dtype x1>)
                   attributes{work_group_size=[16,1]} {
    %gid = group_id.x : index
    %itask_idx = cast %itask : index
    ; Compute task id
    %tid = add %itask_idx, %gid : index

    ; Load task struct
    %c0 = constant 0 : index
    %c1 = constant 1 : index
    %c2 = constant 2 : index
    %c3 = constant 3 : index
    %c4 = constant 4 : index
    %c5 = constant 5 : index
    %iM = load %tasks[%c0, %tid] : i32
    %iN = load %tasks[%c1, %tid] : i32
    %iK = load %tasks[%c2, %tid] : i32
    %ioffset_a = load %tasks[%c3, %tid] : i32
    %ioffset_b = load %tasks[%c4, %tid] : i32
    %ioffset_c = load %tasks[%c5, %tid] : i32

    ; Cast task struct to index type
    %M = cast %iM : index
    %N = cast %iN : index
    %K = cast %iK : index
    %offset_a = cast %ioffset_a : index
    %offset_b = cast %ioffset_b : index
    %offset_c = cast %ioffset_c : index

    ; Get view on small matrices
    %MK = mul %M, %K : index
    %KN = mul %K, %N : index
    %MN = mul %M, %N : index
    %av = subview %A[%offset_a:%MK] : memref<$dtype x?>
    %bv = subview %B[%offset_b:%KN] : memref<$dtype x?>
    %cv = subview %C[%offset_c:%MN] : memref<$dtype x?>
    %a = expand %av[0->%M x %K] : memref<$dtype x?x?,strided<1,?>>
    %b = expand %bv[0->%N x %K] : memref<$dtype x?x?,strided<1,?>>
    %c = expand %cv[0->%M x %N] : memref<$dtype x?x?,strided<1,?>>

    ; GEMM
    %one = constant 1.0 : $dtype
    gemm.atomic.n.t %alpha, %a, %b, %one, %c
}
